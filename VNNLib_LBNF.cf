-- This is the LBNF grammar for the VNNLib language

-- The regular expression for a signed double or integer
token SDouble '-'? digit+ '.' digit+ ('e' '-'? digit+)? ;
token SInt '-' digit+ ;
token Int digit+ ;

-- The regular expression for an element of a tensor variable
token TensorElement letter+ digit* '_' digit+ ('-' digit+)* ;

-- The regular expression for a tensor variable
token VariableName letter+ digit* ;

-- The regular expression for the shape of a tensor
token TensorShape '[' digit+ ((' ')* ',' (' ')* digit+)* ']' ;

-- A parameter is a scalar variable that may be a signed integer or a double
DoubleParam. Parameter ::= SDouble ;
SIntParam. Parameter ::= SInt ;
IntParam. Parameter ::= Int ;

-- An Input is a numeric value that may be a paramater, a tensor element, 
-- or the result of an arithmetic formula
TElemInput. Input ::= TensorElement ;
ParamInput. Input ::= Parameter ;
ArithInput. Input ::= ArithmeticFormula ;

-- A comparative operator is a binary operator that compares two inputs
GreaterThan. ComparativeOperation ::= ">" Input Input ;
LessThan. ComparativeOperation ::= "<" Input Input ;
GreaterEqual. ComparativeOperation ::= ">=" Input Input ;
LessEqual. ComparativeOperation ::= "<=" Input Input ;
NotEqual. ComparativeOperation ::= "!=" Input Input;
Equal. ComparativeOperation ::= "==" Input Input ;

-- An arithmetic operator is an n-ary operator that performs an arithmetic operation on n inputs
ArithFormulaInputBase. ArithmeticInputs ::= Input Input ;
ArithFormulaInput. ArithmeticInputs ::= ArithmeticInputs Input ;

Negate. ArithmeticOperation ::= "-" Input ;
Plus. ArithmeticOperation ::= "+" ArithmeticInputs ;
Minus. ArithmeticOperation ::= "-" ArithmeticInputs ;
Multiply. ArithmeticOperation ::= "*" ArithmeticInputs ;

-- A connective operator is an n-ary operator that combines at least two boolean formulas
ConnBool. Boolean ::= ConnectiveFormula ;
CompBool. Boolean ::= ComparativeFormula ;
ConnFormInputsBase. ConnectiveInputs ::= Boolean Boolean ;
ConnFormInputs. ConnectiveInputs ::= ConnectiveInputs Boolean ;

And. ConnectiveOperation ::= "and" ConnectiveInputs ;
Or. ConnectiveOperation ::= "or" ConnectiveInputs ;

-- Arithmetic, comparative, and connective formulas are bounded by parentheses
ArithForm. ArithmeticFormula ::= "(" ArithmeticOperation")" ;
CompForm. ComparativeFormula ::= "(" ComparativeOperation ")" ;
ConnForm. ConnectiveFormula ::= "(" ConnectiveOperation ")" ;

-- A property is a constraint that must be satisfied by the model. 
-- It is an assertion of an arithmetic, comparative, or connective formula
PropArithmetic. Property ::= "(" "assert" ArithmeticFormula ")" ;
PropComparative. Property ::= "(" "assert" ComparativeFormula ")" ;
PropConnective. Property ::= "(" "assert" ConnectiveFormula ")" ;

MultiplePropBase. MultipleProperty ::= Property ;
MultipleProp. MultipleProperty ::= MultipleProperty Property ;

-- A tensor element is a scalar variable of type Real
-- TODO: Add support for other types
EType. ElementType ::= "Real" ;

-- A tensor definition is a declaration of a tensor variable with a shape and an element type
DefConst. Definition ::= "(" "declare-const" VariableName TensorShape ElementType ")" ;
DefInput. Definition ::= "(" "declare-input" VariableName TensorShape ElementType ")" ;
DefOutput. Definition ::= "(" "declare-output" VariableName TensorShape ElementType ")" ;

-- A tensor definition may be used to refer to a set of intermediate nodes in the network.
-- We consider intermediate nodes as an intermediate output from the network
PosLayerNumber. LayerNumber ::= Int ;
NegLayerNumber. LayerNumber ::= SInt ;
DefLayer. Definition ::= "(" "declare-output" VariableName TensorShape ElementType "layer" ":" LayerNumber ")" ;

-- A tensor definition may be used to refer to an ONNX node in the network.
-- We consider ONNX nodes as an intermediate output from the network
DefOnnxNode. Definition ::= "(" "declare-output" VariableName TensorShape ElementType "onnx-node" ":" String ")" ;

MultipleDefBase. MultipleDefinition ::= Definition ;
MultipleDef. MultipleDefinition ::= MultipleDefinition Definition ;

DefNetwork. Network ::= "(" "declare-network" VariableName "("  MultipleDefinition ")" ")" ;
MultipleNetworkBase. MultipleNetwork ::= Network ;
MultipleNetwork. MultipleNetwork ::= MultipleNetwork Network ;

-- A command consists a series of networks, followed by a series of definitions followed by a series of properties
Comm1. Command ::= MultipleNetwork MultipleDefinition MultipleProperty ;
Comm2. Command ::= MultipleNetwork MultipleProperty ;
Comm3. Command ::= MultipleDefinition MultipleProperty ;

-- A line may be commented out by starting with a semicolon
comment ";" ;

entrypoints Command ;
