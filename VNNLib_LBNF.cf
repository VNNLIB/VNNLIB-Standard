-- This is the LBNF grammar for the VNNLib language

-- The regular expression for a signed double or integer
token SDouble '-'? digit+ '.' digit+ ('e' '-'? digit+)? ;
token SInt '-' digit+ ;
token Int digit+ ;

-- The regular expression for a network's name
token Network letter+ digit* ;

-- The regular expression for an element of a tensor variable
token TensorElement letter+ digit* '_' digit+ ('-' digit+)* ;

-- The regular expression for a tensor variable
token TensorVariable letter+ digit* ;

-- The regular expression for the shape of a tensor
token TensorShape '[' digit+ (',' digit+)* ']' ;

-- A parameter is a scalar variable that may be a signed integer or a double
DoubleParam. Parameter ::= SDouble ;
SIntParam. Parameter ::= SInt ;
IntParam. Parameter ::= Int ;

-- An Input is a numeric value that may be a paramater, a tensor element, 
-- or the result of an arithmetic formula
TElemInput. Input ::= TensorElement ;
ParamInput. Input ::= Parameter ;
ArithInput. Input ::= ArithmeticFormula ;

-- A comparative operator is a binary operator that compares two inputs
GreaterThan. ComparativeOperation ::= ">" Input Input ;
LessThan. ComparativeOperation ::= "<" Input Input ;
GreaterEqual. ComparativeOperation ::= ">=" Input Input ;
LessEqual. ComparativeOperation ::= "<=" Input Input ;
NotEqual. ComparativeOperation ::= "!=" Input Input;
Equal. ComparativeOperation ::= "==" Input Input ;

-- An arithmetic operator is an n-ary operator that performs an arithmetic operation on n inputs
ArithFormulaInputBase. ArithmeticInputs ::= Input Input ;
ArithFormulaInput. ArithmeticInputs ::= ArithmeticInputs Input ;

Plus. ArithmeticOperation ::= "+" ArithmeticInputs ;
Minus. ArithmeticOperation ::= "-" ArithmeticInputs ;
Multiply. ArithmeticOperation ::= "*" ArithmeticInputs ;

-- A connective operator is an n-ary operator that combines at least two boolean formulas
ConnBool. Boolean ::= ConnectiveFormula ;
CompBool. Boolean ::= ComparativeFormula ;
ConnFormInputsBase. ConnectiveInputs ::= Boolean Boolean ;

-- A legacy connective operator used for compatibility with the original VNNLib format
ConnFormInputsBaseLegacy. ConnectiveInputs ::= Boolean ;

ConnFormInputs. ConnectiveInputs ::= ConnectiveInputs Boolean ;

And. ConnectiveOperation ::= "and" ConnectiveInputs ;
Or. ConnectiveOperation ::= "or" ConnectiveInputs ;

-- Arithmetic, comparative, and connective formulas are bounded by parentheses
ArithForm. ArithmeticFormula ::= "(" ArithmeticOperation")" ;
CompForm. ComparativeFormula ::= "(" ComparativeOperation ")" ;
ConnForm. ConnectiveFormula ::= "(" ConnectiveOperation ")" ;

-- A property is a constraint that must be satisfied by the model. 
-- It is an assertion of an arithmetic, comparative, or connective formula
PropArithmetic. Property ::= "(" "assert" ArithmeticFormula ")" ;
PropComparative. Property ::= "(" "assert" ComparativeFormula ")" ;
PropConnective. Property ::= "(" "assert" ConnectiveFormula ")" ;

MultiplePropBase. MultipleProperty ::= Property ;
MultipleProp. MultipleProperty ::= MultipleProperty Property ;

-- A tensor element is a scalar variable of type Real
-- TODO: Add support for other types
EType. ElementType ::= "Real" ;

-- A definition is a declaration of a constant variable or a function variable

-- A definition can be used to declare a tensor variable using the shape of the tensor
ExpandedDefConst. ExpandedDefinition ::= "(" "declare-const" TensorElement ElementType ")" ;
ExpandedDefInput. ExpandedDefinition ::= "(" "declare-input" TensorElement ElementType ")" ;
ExpandedDefOutput. ExpandedDefinition ::= "(" "declare-output" TensorElement ElementType ")" ;

-- A legacy definition used for compatibility with the original VNNLib format
ExpandedDefFun. ExpandedDefinition ::= "(" "declare-fun" TensorElement "()" ElementType ")" ;

DefConst. Definition ::= "(" "declare-const" TensorVariable TensorShape ElementType ")" ;
DefInput. Definition ::= "(" "declare-input" TensorVariable TensorShape ElementType ")" ;
DefOutput. Definition ::= "(" "declare-output" TensorVariable TensorShape ElementType ")" ;
Def. Definition ::= ExpandedDefinition ;

MultipleDefBase. MultipleDefinition ::= Definition ;
MultipleDef. MultipleDefinition ::= MultipleDefinition Definition ;

-- A command consists a series of definitions followed by a series of properties
Comm. Command ::= MultipleDefinition MultipleProperty ;

-- A line may be commented out by starting with a semicolon
comment ";" ;

entrypoints Command ;
