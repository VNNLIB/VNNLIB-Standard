token SDouble '-'? digit+ '.' digit+ ('e' '-'? digit+)? ;
token SInt '-'? digit+ ;
token TensorElement letter+ digit* ('_' digit+ ('-' digit+)*)? ;

TElem. Element ::= TensorElement ;

DParam. Parameter ::= SDouble ;
IParam. Parameter ::= SInt ;

GreaterThan. ComparativeOperator ::= ">" ;
LessThan. ComparativeOperator ::= "<" ;
GreaterEqual. ComparativeOperator ::= ">=" ;
LessEqual. ComparativeOperator ::= "<=" ;
NotEqual. ComparativeOperator ::= "!=" ;
Equal. ComparativeOperator ::= "==" ;

And. Connective ::= "and" ;
Or. Connective ::= "or" ;
Not. Connective ::= "not" ;

Plus. ArithmeticOperator ::= "+" ;
Minus. ArithmeticOperator ::= "-" ;
Multiply. ArithmeticOperator ::= "*" ;

ElemInput. Input ::= Element ;
ParamInput. Input ::= Parameter ;

AFormBase. ArithmeticFormula ::= "(" ArithmeticOperator Input Input ")" ;
AForm1. ArithmeticFormula ::= "(" ArithmeticOperator ArithmeticFormula Input ")" ;
AForm2. ArithmeticFormula ::= "(" ArithmeticOperator Input ArithmeticFormula ")" ;
AForm3. ArithmeticFormula ::= "(" ArithmeticOperator ArithmeticFormula ArithmeticFormula ")" ;

CompFormBase. ComparativeFormula ::= "(" ComparativeOperator Input Input ")" ;
CompForm1. ComparativeFormula ::= "(" ComparativeOperator ArithmeticFormula Input ")" ;
CompForm2. ComparativeFormula ::= "(" ComparativeOperator Input ArithmeticFormula ")" ;
CompForm3. ComparativeFormula ::= "(" ComparativeOperator ArithmeticFormula ArithmeticFormula ")" ;

ConnFormBase. ConnectiveFormula ::= "(" Connective ComparativeFormula ")" ;
BoolForm1. BooleanFormula ::= ConnectiveFormula;
BoolForm2. BooleanFormula ::= ComparativeFormula;
BoolForm3. BooleanFormula ::= BooleanFormula ConnectiveFormula;
BoolForm4. BooleanFormula ::= BooleanFormula ComparativeFormula;
ConnForm. ConnectiveFormula ::= "(" Connective BooleanFormula ")" ;

Form1. Formula ::= ArithmeticFormula ;
Form2. Formula ::= ComparativeFormula ;
Form3. Formula ::= ConnectiveFormula ;

EType. ElementType ::= "Real" ;
CDef. Definition ::= "(" "declare-const" Element ElementType ")" ;
FDef. Definition ::= "(" "declare-fun" Element "()" ElementType ")" ;

Con. Constraint ::= "(" "assert" Formula ")" ;

CommDef. Command ::= Definition ;
CommCon. Command ::= Constraint ;
Specs1. Command ::= Command "\n" Definition;
Specs2. Command ::= Command "\n" Constraint;

comment ";" ;

entrypoints Command;